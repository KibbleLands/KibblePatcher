import java.nio.file.Files
import java.nio.file.StandardCopyOption

plugins {
    id 'java'
}

group 'org.example'
version '1.7-dev03'

targetCompatibility = 1.8
sourceCompatibility = 1.8

repositories {
    mavenCentral()
    // Bukkit dependency
    maven { url = 'https://repo.destroystokyo.com/repository/maven-public/' }
    maven { url = 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
}

configurations {
    shadow
}

jar {
    from "LICENSE"

    manifest {
        attributes 'Main-Class': 'net.kibblelands.patcher.Main'
        attributes 'KibblePatcher-Version': "${project.version}"
        attributes 'KibblePatcher-Provider': 'KibbleLands'
    }
}

afterEvaluate {
    jar {
        from(configurations.shadow.collect { it.isDirectory() ? it : zipTree(it) }) {
            exclude("module-info.class")
            exclude("org/fusesource/jansi/internal/native/FreeBSD/**")
            exclude("org/fusesource/jansi/internal/native/Linux/**")
            exclude("org/fusesource/jansi/internal/native/Mac/**")
        }
    }
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    implementation 'org.ow2.asm:asm-commons:9.1'
    shadow 'org.ow2.asm:asm-commons:9.1'
    implementation 'org.fusesource.jansi:jansi:2.3.1'
    shadow 'org.fusesource.jansi:jansi:2.3.1'
    // Bukkit dependency (Only used in net.kibblelands.server.*)
    compileOnly('com.destroystokyo.paper:paper-api:1.16.5-R0.1-SNAPSHOT')
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

//make builds reproducible
tasks.withType(AbstractArchiveTask) {
    reproducibleFileOrder = true
    preserveFileTimestamps = false
}

task buildRoot(type: Task) {
    doLast {
        Files.copy(new File(project.buildDir, "libs/KibblePatcher-${version}.jar").toPath(),
        new File(projectDir, "KibblePatcher.jar").toPath(), StandardCopyOption.REPLACE_EXISTING)
    }
    dependsOn(jar)
}