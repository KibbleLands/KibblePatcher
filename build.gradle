import org.gradle.api.internal.tasks.DefaultTaskDependency

import java.nio.file.Files
import java.nio.file.StandardCopyOption

plugins {
    id 'java'
}

group 'org.example'
version '1.4'

repositories {
    mavenCentral()
    // Bukkit dependency
    maven { url = 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
}

configurations {
    shadow
}

jar {
    from "LICENSE"

    manifest {
        attributes 'Main-Class': 'net.kibblelands.patcher.Main'
        attributes 'KibblePatcher-Version': "${project.version}"
        attributes 'KibblePatcher-Provider': 'KibbleLands'
    }
}

afterEvaluate {
    jar {
        from(configurations.shadow.collect { it.isDirectory() ? it : zipTree(it) }) {
            exclude("module-info.class")
            exclude("org/fusesource/jansi/internal/native/FreeBSD/**")
            exclude("org/fusesource/jansi/internal/native/Linux/**")
            exclude("org/fusesource/jansi/internal/native/Mac/**")
        }
    }
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    implementation 'org.ow2.asm:asm-commons:9.0'
    shadow 'org.ow2.asm:asm-commons:9.0'
    implementation 'org.fusesource.jansi:jansi:2.1.0'
    shadow 'org.fusesource.jansi:jansi:2.1.0'
    // Bukkit dependency (Only used in net.kibblelands.server.*)
    compileOnly 'org.jetbrains:annotations:16.0.2'
    compileOnly('org.spigotmc:spigot-api:1.16.5-R0.1-SNAPSHOT') {
        transitive = false
    }
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

task buildRoot(type: Task) {
    doLast {
        Files.copy(new File(project.buildDir, "libs/KibblePatcher-${version}.jar").toPath(),
        new File(projectDir, "KibblePatcher.jar").toPath(), StandardCopyOption.REPLACE_EXISTING)
    }
    dependsOn(jar)
}

/*
jar.enabled = false

(build.getTaskDependencies() as DefaultTaskDependency).getMutableValues().clear()
build.dependsOn(finalJar, test)
 */